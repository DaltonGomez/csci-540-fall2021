# More interesting queries

Today, we will study some more sophisticated queries.  First lets clean up from
last time:

    match (n)
    optional match (n)-[r]-()
    delete n,r

And load a movie graph.  Run `:play movie-graph` and follow the directions to
load.

Lets start off by taking a look at the types of relationships in the graph

    match ()-[r]-()
    return distinct type(r);

We can also look at labels of nodes and count the number of each

    match (n)
    return distinct labels(n), count(n);

Now, let's look at more complex queries... We will start with the "Kevin Bacon
Game" ... only with Tom Hanks.

As a warmup, find everyone related to the "The Divinci Code"

    match (m:Movie {title: "The Da Vinci Code"}) -[] - (n) return m,n

Now, just those that acted in it

    match (m:Movie {title: "The Da Vinci Code"}) -[:ACTED_IN] - (n) return n

Next, let's look up all the movies that Tom Hanks has been in

    match (tom:Person {name: "Tom Hanks"}) -[] - (n) return tom, n

And everyone that worked with tom:

    match (tom:Person {name: "Tom Hanks"}) - [:ACTED_IN] ->
        (m) <- [:ACTED_IN] - (n)
    return tom, m, n;

Okay, but going out further starts getting old quickly, luckily there is a
shorthand:

    match (tom:Person {name: "Tom Hanks"}) -[*1..2] - (n) return tom, n

Let's count them all

    match (tom:Person {name: "Tom Hanks"}) -[*1..2] - (n) return count(n)

But... Look at the graph... A talented thespian like Meg Ryan is in multiple
paths, which means that she will counted multiple times.  Let's confirm


**Question: Write a query to test if Meg Ryan is counted multiple times**

    match (tom:Person {name: "Tom Hanks"}) -[*1..2] - (n:Person {name: "Meg Ryan"})
    return count(n)

So we need to use the modifier DISTINCT

    match (tom:Person {name: "Tom Hanks"}) -[*1..2] - (n:Person {name: "Meg Ryan"})
    return count(DISTINCT n)

Okay, now, lets count

    match (tom:Person {name: "Tom Hanks"}) -[*1..2] - (n)
    return count(DISTINCT n)

And count actors that acted with actors that acted with tom

    match (tom:Person {name: "Tom Hanks"}) -[*1..3] - (n)
    return count(DISTINCT n)

And we can continue...

But we can do all this with a relational DB!  But, here we can do shortest path
queries.  Let's start off by looking at the amazing duo of Tom and Meg!

    match
        (tom:Person {name: "Tom Hanks"}),
        (meg:Person {name: "Meg Ryan"}),
         p = shortestPath( (tom) -[:ACTED_IN*] - (meg) )
    return p

Or, just get its length:

    match
        (tom:Person {name: "Tom Hanks"}),
        (meg:Person {name: "Meg Ryan"}),
         p = shortestPath( (tom) -[:ACTED_IN*] - (meg) )
    return length(p)

Great!  Now, let's look at the proportion of the actors in our DB that are
within 2 acting hops of tom.  First, lets get back to shortest path with 2 hops
so all those that acted with Tom

    match
        (tom:Person {name: "Tom Hanks"}),
         p = shortestPath( (tom) -[:ACTED_IN*1..2] - (other:Person) )
    where
        other <> tom
    return length(p)

Great!  Now, let's look at the proportion of the actors in our DB that are
within 2 acting hops of tom.  And then all those that acted with acted with tom

    match
        (tom:Person {name: "Tom Hanks"}),
         p = shortestPath( (tom) -[:ACTED_IN*1..4] - (other:Person) )
    where
        other <> tom
    return length(p)

**Question** why is acted with 2 and acted with acted with 4?

Okay, let's finish up with getting the total number of people in the DB that are
not tom

    match (p:Person)
    where
        p.name <> "Tom Hanks"
    return count(p)

And the percent of people in our DB how many acted with actors that acted with
tom

    match
        (tom:Person {name: "Tom Hanks"}),
         p = shortestPath( (tom) -[:ACTED_IN*1..4] - (other:Person) )
    where
        other <> tom
    return count(p)/132.0

And if we go 3 acting hops (path length 1..6) we see more connections.

## REST and Cypher

One final point, you can use the REST API with cypher.  To make sure all is
running, run:

    curl http://localhost:7474/db/data/cypher

From the terminal run:

    curl -X POST \
        http://localhost:7474/db/data/cypher \
        -H "Content-Type: application/json" \
        -d '{"query":"MATCH (n:Person) RETURN n.name;"}'

From there, I suggest using tools like `httpie` or postman, but you-do-you.
